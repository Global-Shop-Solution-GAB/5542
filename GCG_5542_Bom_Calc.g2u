Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Size(5790,3540)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Caption("BOM_Name")
Gui.Form..Sizeable(False)
Gui.Form..MaxButton(False)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form.frmHeader.Create(Frame)
Gui.Form.frmHeader.Size(2385,930)
Gui.Form.frmHeader.Position(3135,90)
Gui.Form.lbl2.Create(Label,"RM Cost",True,870,255,0,150,240,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl2.Parent("frmHeader")
Gui.Form.lbl2.FontStyle(True,False,False,False,False)
Gui.Form.txtRMCost.Create(TextBox,"",True,990,300,0,90,465,True,2,"Arial",8,-2147483643,1)
Gui.Form.txtRMCost.Parent("frmHeader")
Gui.Form.txtRMCost.Locked(True)
Gui.Form.lbl3.Create(Label,"Lot Cost",True,885,255,0,1320,240,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl3.Parent("frmHeader")
Gui.Form.lbl3.FontStyle(True,False,False,False,False)
Gui.Form.txtLotCost.Create(TextBox,"",True,990,300,0,1275,465,True,2,"Arial",8,-2147483643,1)
Gui.Form.txtLotCost.Parent("frmHeader")
Gui.Form.txtLotCost.Locked(True)
Gui.Form.frame1.Create(Frame)
Gui.Form.frame1.Size(1140,930)
Gui.Form.frame1.Position(105,90)
Gui.Form.lbl4.Create(Label,"ZK",True,390,255,0,300,240,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl4.Parent("frame1")
Gui.Form.lbl4.FontStyle(True,False,False,False,False)
Gui.Form.txtZK.Create(TextBox,"",True,945,300,0,90,465,True,2,"Arial",8,-2147483643,1)
Gui.Form.txtZK.Parent("frame1")
Gui.Form.txtZK.NumericOnly(2)
Gui.Form.GsGridControl1.Create(GsGridControl)
Gui.Form.GsGridControl1.Size(5400,1320)
Gui.Form.GsGridControl1.Position(105,1125)
Gui.Form.cmdRefresh.Create(Button)
Gui.Form.cmdRefresh.Size(855,375)
Gui.Form.cmdRefresh.Position(4650,2505)
Gui.Form.cmdRefresh.Caption("Refresh")
Gui.Form.cmdRefresh.Event(Click,refreshValues)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
v.Global.sBOM.Declare
v.Global.fRM_Cost.Declare
v.Global.fLot_Cost.Declare
v.Global.fLot_Factor.Declare

v.Global.fLot_1MO.Declare
v.Global.fLot_6MO.Declare
v.Global.fLot_13MO.Declare

v.Global.fVar_1MO.Declare
v.Global.fVar_6MO.Declare
v.Global.fVar_13MO.Declare

'v.Global.sBOM.Set("BOM_PARENT_1")
Program.Sub.Preflight.End

Program.Sub.Main.Start
v.Local.sTemp.Declare
f.Intrinsic.Control.Try
	f.Intrinsic.Control.SelectCase(v.Caller.Hook)
		f.Intrinsic.Control.Case(16300)
			v.Passed.000214.Set("Lot Factor")
			f.Intrinsic.Control.End
		f.Intrinsic.Control.Case(16360)
			f.Intrinsic.String.RPad(v.Passed.000001, " ", 17, v.Local.sTemp)
			f.Intrinsic.String.Build("{0}{1}", v.Local.sTemp, v.Passed.000093, v.Global.sBOM)
			
			f.Intrinsic.Control.If(v.Global.sBOM.Trim, =, "")
				f.Intrinsic.UI.Msgbox("Please select a BOM.")
				f.Intrinsic.Control.End
			f.Intrinsic.Control.EndIf
			
			f.Intrinsic.Control.CallSub(createEmptyDisplayDT)
			f.Intrinsic.Control.CallSub(loadProperties)
			f.Intrinsic.Control.CallSub(createEmptyDT)	
			f.Intrinsic.Control.CallSub(createDictionary)
			f.Intrinsic.Control.CallSub(getBomParent)
			gui.Form..Show
			
		f.Intrinsic.Control.Case(16310)
			f.Intrinsic.String.RPad(v.Passed.000001, " ", 17, v.Local.sTemp)
			f.Intrinsic.String.Build("{0}{1}", v.Local.sTemp, v.Passed.000093, v.Global.sBOM)
			
			f.Intrinsic.Control.If(v.Global.sBOM.Trim, =, "")
				f.Intrinsic.UI.Msgbox("Please select a BOM.")
				f.Intrinsic.Control.End
			f.Intrinsic.Control.EndIf
			
			f.Intrinsic.Control.CallSub(createEmptyDisplayDT)
			f.Intrinsic.Control.CallSub(loadProperties)
			f.Intrinsic.Control.CallSub(createEmptyDT)	
			f.Intrinsic.Control.CallSub(createDictionary)
			f.Intrinsic.Control.CallSub(getBomParent)	
			f.Intrinsic.Control.CallSub(saveToDB)
			f.Intrinsic.Control.End
	f.Intrinsic.Control.EndSelect
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Closes ODBC connection, default to connection : "con"
f.Intrinsic.Control.If(v.ODBC!con.State, =, 1)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.EndIf

'Generic Error message.
f.Intrinsic.String.Build("Project ProjectName.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.End
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Form_UnLoad.End

Program.Sub.createEmptyDisplayDT.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.If(v.DataTable.displayDT.Exists)
		f.Data.DataTable.Close("displayDT")
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.Create("displayDT", true)
	f.Data.DataTable.AddColumn("displayDT", "type_Col", "String")
	f.Data.DataTable.AddColumn("displayDT", "col_1", "float")
	f.Data.DataTable.AddColumn("displayDT", "col_6", "float")
	f.Data.DataTable.AddColumn("displayDT", "col_13", "float")
	
	f.Data.DataTable.SetValueFormat("displayDT", -1, "col_1", "col_1", "#.##")
	f.Data.DataTable.SetValueFormat("displayDT", -1, "col_6", "col_6", "#.##")
	f.Data.DataTable.SetValueFormat("displayDT", -1, "col_13", "col_13", "#.##")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createEmptyDisplayDT.End

Program.Sub.loadProperties.Start
f.Intrinsic.Control.Try
	gui.Form.GsGridControl1.AddGridviewFromDatatable("displayGV", "displayDT")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "caption", " ")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "caption", "1% MO")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "caption", "6% MO")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "caption", "13% MO")
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "HeaderFontBold", True)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "HeaderFontBold", True)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "HeaderFontBold", True)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "HeaderFontBold", True)
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "HeaderHAlignment", "Near")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "HeaderHAlignment", "Center")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "HeaderHAlignment", "Center")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "HeaderHAlignment", "Center")
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "CellHAlignment", "Near")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "CellHAlignment", "Center")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "CellHAlignment", "Center")
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "CellHAlignment", "Center")
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "HeaderForeColor", v.Color.White)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "HeaderForeColor", v.Color.White)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "HeaderForeColor", v.Color.White)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "HeaderForeColor", v.Color.White)
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "HeaderBackColor", v.Color.GSS)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "HeaderBackColor", v.Color.GSS)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "HeaderBackColor", v.Color.GSS)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "HeaderBackColor", v.Color.GSS)
	
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "type_Col", "Width", 120)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_1", "Width", 80)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_6", "Width", 80)
	gui.Form.GsGridControl1.SetColumnProperty("displayGV", "col_13", "Width", 80)
	
	gui.Form.GsGridControl1.MainView("displayGV")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.loadProperties.End

Program.Sub.createEmptyDT.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.If(v.DataTable.RM_CostDT.Exists)
		f.Data.DataTable.Close("RM_CostDT")
	f.Intrinsic.Control.EndIf	
	f.Data.DataTable.Create("RM_CostDT", true)
	f.Data.DataTable.AddColumn("RM_CostDT", "Part", "String")
	f.Data.DataTable.AddColumn("RM_CostDT", "QTY", "Float")
	f.Data.DataTable.AddColumn("RM_CostDT", "UnitCost", "Float")
	
	f.Intrinsic.Control.If(v.DataTable.Lot_CostDT.Exists)
		f.Data.DataTable.Close("Lot_CostDT")
	f.Intrinsic.Control.EndIf	
	f.Data.DataTable.Create("Lot_CostDT", true)
	f.Data.DataTable.AddColumn("Lot_CostDT", "Part", "String")
	f.Data.DataTable.AddColumn("Lot_CostDT", "QTY", "Float")
	f.Data.DataTable.AddColumn("Lot_CostDT", "UnitCost", "Float")
	
	f.Intrinsic.Control.If(v.DataTable.saveDT.Exists)
		f.Data.DataTable.Close("saveDT")
	f.Intrinsic.Control.EndIf	
	f.Data.DataTable.Create("saveDT", true)
	f.Data.DataTable.AddColumn("saveDT", "BomParent", "String")
	f.Data.DataTable.AddColumn("saveDT", "PL", "String")
	f.Data.DataTable.AddColumn("saveDT", "RmCost", "Float")
	f.Data.DataTable.AddColumn("saveDT", "LotCost", "Float")
	f.Data.DataTable.AddColumn("saveDT", "LotFactor", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Lot_1MO", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Lot_6MO", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Lot_13MO", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Var_1MO", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Var_6MO", "Float")
	f.Data.DataTable.AddColumn("saveDT", "Var_13MO", "Float")
	
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Lot_1MO", "Lot_1MO", "#.##")
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Lot_6MO", "Lot_6MO", "#.##")
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Lot_13MO", "Lot_13MO", "#.##")
	
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Var_1MO", "Var_1MO", "#.##")
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Var_6MO", "Var_6MO", "#.##")
	f.Data.DataTable.SetValueFormat("saveDT", -1, "Var_13MO", "Var_13MO", "#.##")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createEmptyDT.End

Program.Sub.createDictionary.Start
f.Intrinsic.Control.Try
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.Dictionary.CreateFromSQL("productLineDict", "con", "Select Part, Product_Line from V_Inventory_Mstr")
		f.Data.Dictionary.SetDefaultReturn("productLineDict", "")
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createDictionary.End

Program.Sub.getBomParent.Start
v.Local.sSQL.Declare
v.Local.iCnt.Declare
v.Local.sCurrentPart.Declare
v.Local.sPL.Declare
v.Local.fCurrentQty.Declare
v.Local.fCurrentCost.Declare
f.Intrinsic.Control.Try

	f.Intrinsic.String.Build("Select Parent, Sequence_Bom, Cost, Quantity, Part, Part as PL from V_BOM_MSTR where Parent  =  '{0}' and Sequence_Bom <> '0000' order by Parent, Sequence_BOM", v.Global.sBOM, v.Local.sSQL)
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.CreateFromSQL("bomLevel_1DT", "con", v.Local.sSQL)
		f.Data.DataTable.FillFromDictionary("bomLevel_1DT", "productLineDict", "PL")
	f.ODBC.Connection!con.Close
	
	f.Intrinsic.Control.For(v.Local.iCnt, 0, v.DataTable.bomLevel_1DT.RowCount--, 1)
		v.Local.sCurrentPart.Set(v.DataTable.bomLevel_1DT(v.Local.iCnt).Part!FieldVal)
		v.Local.sPL.Set(v.DataTable.bomLevel_1DT(v.Local.iCnt).PL!FieldVal)
		v.Local.fCurrentQty.Set(v.DataTable.bomLevel_1DT(v.Local.iCnt).Quantity!FieldVal)
		v.Local.fCurrentCost.Set(v.DataTable.bomLevel_1DT(v.Local.iCnt).Cost!FieldVal)
		
		f.Intrinsic.String.Build("Select Parent, Sequence_Bom, Cost, Quantity, Part, Part as PL from V_BOM_MSTR where Parent  =  '{0}' and Sequence_Bom <> '0000' order by Parent, Sequence_BOM", v.Local.sCurrentPart, v.Local.sSQL)
		f.ODBC.Connection!con.OpenCompanyConnection
			f.Data.DataTable.CreateFromSQL("bomChildDT", "con", v.Local.sSQL)
		f.ODBC.Connection!con.Close
		
		f.Intrinsic.Control.If(v.DataTable.bomChildDT.RowCount, >, 0)
			f.Intrinsic.Control.CallSub(getBomChild, "passedQTY", v.Local.fCurrentQty, "passedBom", v.Local.sCurrentPart)
			
		f.Intrinsic.Control.Else
			f.Intrinsic.Control.If(v.Local.sPL.Trim, =, "RM")
				f.Data.DataTable.AddRow("RM_CostDT", "Part", v.Local.sCurrentPart, "QTY", v.Local.fCurrentQty, "UnitCost", v.Local.fCurrentCost)
			f.Intrinsic.Control.EndIf
			
			f.Intrinsic.Control.If(v.Local.sPL.Trim, =, "ZL")
				f.Data.DataTable.AddRow("Lot_CostDT", "Part", v.Local.sCurrentPart, "QTY", v.Local.fCurrentQty, "UnitCost", v.Local.fCurrentCost)
			f.Intrinsic.Control.EndIf
			
			f.Intrinsic.Control.If(v.Local.sPL.Trim, =, "ZK")
				f.Intrinsic.Math.Mult(v.Local.fCurrentQty, v.Local.fCurrentCost, v.Global.fLot_Factor)
			f.Intrinsic.Control.EndIf
		f.Intrinsic.Control.EndIf
		
			f.Data.DataTable.Close("bomChildDT")
	f.Intrinsic.Control.Next(v.Local.iCnt)
	
	f.Intrinsic.Control.CallSub(calculateValues)
	'f.Intrinsic.Control.CallSub(saveToDB)
	f.Data.DataTable.Close("bomLevel_1DT")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.getBomParent.End

Program.Sub.getBomChild.Start
v.Local.sSQL.Declare
v.Local.iCnt.Declare
v.Local.sCurrentPart.Declare
v.Local.sPL.Declare
v.Local.fCurrentQty.Declare
v.Local.fCurrentCost.Declare
f.Intrinsic.Control.Try
	f.Intrinsic.String.Build("Select Parent, Sequence_Bom, Cost, Quantity, Part, Part as PL from V_BOM_MSTR where Parent  =  '{0}' and Sequence_Bom <> '0000' order by Parent, Sequence_BOM", v.Args.passedBom, v.Local.sSQL)
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.CreateFromSQL("subParentDT", "con", v.Local.sSQL)
		'f.Data.Dictionary.CreateFromSQL("productLineDict", "con", "Select Part, Product_Line from V_Inventory_Mstr")
		'f.Data.Dictionary.SetDefaultReturn("productLineDict", "")
		f.Data.DataTable.FillFromDictionary("subParentDT", "productLineDict", "PL")
		'f.Data.Dictionary.Close("productLineDict")
	f.ODBC.Connection!con.Close
	
	f.Intrinsic.Control.For(v.Local.iCnt, 0, v.DataTable.subParentDT.RowCount--, 1)
		v.Local.sCurrentPart.Set(v.DataTable.subParentDT(v.Local.iCnt).Part!FieldVal)
		v.Local.sPL.Set(v.DataTable.subParentDT(v.Local.iCnt).PL!FieldVal)
		v.Local.fCurrentQty.Set(v.DataTable.subParentDT(v.Local.iCnt).Quantity!FieldVal)
		v.Local.fCurrentCost.Set(v.DataTable.subParentDT(v.Local.iCnt).Cost!FieldVal)
		f.Intrinsic.Math.Mult(v.Local.fCurrentQty, v.Args.passedQTY, v.Local.fCurrentQTY)
		
		f.Intrinsic.String.Build("Select Parent, Sequence_Bom, Cost, Quantity, Part, Part as PL from V_BOM_MSTR where Parent  =  '{0}' and Sequence_Bom <> '0000' order by Parent, Sequence_BOM", v.Local.sCurrentPart, v.Local.sSQL)
		f.ODBC.Connection!con.OpenCompanyConnection
			f.Data.DataTable.CreateFromSQL("bomChildDT", "con", v.Local.sSQL)
		f.ODBC.Connection!con.Close
		
		f.Intrinsic.Control.If(v.DataTable.bomChildDT.RowCount, >, 0)
			f.Intrinsic.Control.CallSub(getBomChild, "passedQTY", v.Local.fCurrentQty, "passedBom", v.Local.sCurrentPart)
		f.Intrinsic.Control.Else
			f.Intrinsic.Control.If(v.Local.sPL.Trim, =, "RM")
				f.Data.DataTable.AddRow("RM_CostDT", "Part", v.Local.sCurrentPart, "QTY", v.Local.fCurrentQty, "UnitCost", v.Local.fCurrentCost)
			f.Intrinsic.Control.EndIf
			
			f.Intrinsic.Control.If(v.Local.sPL.Trim, =, "ZL")
				f.Data.DataTable.AddRow("Lot_CostDT", "Part", v.Local.sCurrentPart, "QTY", v.Local.fCurrentQty, "UnitCost", v.Local.fCurrentCost)
			f.Intrinsic.Control.EndIf
		f.Intrinsic.Control.EndIf
		
			f.Data.DataTable.Close("bomChildDT")
	f.Intrinsic.Control.Next(v.Local.iCnt)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.getBomChild.End

Program.Sub.calculateValues.Start
v.Local.fTemp.Declare
f.Intrinsic.Control.Try
	f.Data.DataTable.AddExpressionColumn("RM_CostDT", "TotalCost", "Float", "QTY*UnitCost")
	f.Data.DataTable.Compute("RM_CostDT", "SUM(TotalCost)", "", v.Global.fRM_Cost)
	f.Data.DataTable.AddExpressionColumn("Lot_CostDT", "TotalCost", "Float", "QTY*UnitCost")
	f.Data.DataTable.Compute("Lot_CostDT", "SUM(TotalCost)", "", v.Global.fLot_Cost)
	
	f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.01, v.Local.fTemp)
	f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_1MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_1MO, v.Global.fVar_1MO)
	
	f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.06, v.Local.fTemp)
	f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_6MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_6MO, v.Global.fVar_6MO)
	
	f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.13, v.Local.fTemp)
	f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_13MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_13MO, v.Global.fVar_13MO)
	
	f.Data.DataTable.AddRow("saveDT", "BomParent", v.Global.sBOM, "RmCost", v.Global.fRM_Cost, "LotCost", v.Global.fLot_Cost, "LotFactor", v.Global.fLot_Factor, "Lot_1MO", v.Global.fLot_1MO, "Lot_6MO", v.Global.fLot_6MO, "Lot_13MO", v.Global.fLot_13MO, "Var_1MO", v.Global.fVar_1MO, "Var_6MO", v.Global.fVar_6MO, "Var_13MO", v.Global.fVar_13MO)
	f.Data.DataTable.FillFromDictionary("saveDT", "productLineDict", "PL")
	
	gui.Form..Caption(v.Global.sBOM)
	gui.Form.txtRMCost.Text(v.Global.fRM_Cost)
	gui.Form.txtLotCost.Text(v.Global.fLot_Cost)
	gui.Form.txtZK.Text(v.Global.fLot_Factor)
	f.Data.DataTable.AddRow("displayDT", "type_Col", "LOTFACTOR Calc", "col_1", v.Global.fLot_1MO, "col_6", v.Global.fLot_6MO, "col_13", v.Global.fLot_13MO)
	f.Data.DataTable.AddRow("displayDT", "type_Col", "$ Var to LOTFACTOR", "col_1", v.Global.fVar_1MO, "col_6", v.Global.fVar_6MO, "col_13", v.Global.fVar_13MO)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.calculateValues.End

Program.Sub.refreshValues.Start
v.Local.fTemp.Declare
v.Local.sTempText.Declare
f.Intrinsic.Control.Try
	v.Global.fLot_Factor.Set(v.Screen.Form!txtZK.Text)
	
	'f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.01, v.Local.fTemp)
	'f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_1MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_1MO, v.Global.fVar_1MO)
	
	'f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.06, v.Local.fTemp)
	'f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_6MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_6MO, v.Global.fVar_6MO)
	
	'f.Intrinsic.Math.Mult(v.Global.fRM_Cost, 0.13, v.Local.fTemp)
	'f.Intrinsic.Math.Sub(v.Local.fTemp, v.Global.fLot_Cost, v.Global.fLot_13MO)
	f.Intrinsic.Math.Sub(v.Global.fLot_Factor, v.Global.fLot_13MO, v.Global.fVar_13MO)
	
	f.Data.DataTable.DeleteRow("saveDT")
	f.Data.DataTable.AcceptChanges("saveDT")
	f.Data.DataTable.DeleteRow("displayDT")
	f.Data.DataTable.AcceptChanges("saveDT")
	
	f.Data.DataTable.AddRow("saveDT", "BomParent", v.Global.sBOM, "RmCost", v.Global.fRM_Cost, "LotCost", v.Global.fLot_Cost, "LotFactor", v.Global.fLot_Factor, "Lot_1MO", v.Global.fLot_1MO, "Lot_6MO", v.Global.fLot_6MO, "Lot_13MO", v.Global.fLot_13MO, "Var_1MO", v.Global.fVar_1MO, "Var_6MO", v.Global.fVar_6MO, "Var_13MO", v.Global.fVar_13MO)
	f.Data.DataTable.FillFromDictionary("saveDT", "productLineDict", "PL")
	
	gui.Form..Caption(v.Global.sBOM)
	gui.Form.txtRMCost.Text(v.Global.fRM_Cost)
	gui.Form.txtLotCost.Text(v.Global.fLot_Cost)

	f.Data.DataTable.AddRow("displayDT", "type_Col", "LOTFACTOR Calc", "col_1", v.Global.fLot_1MO, "col_6", v.Global.fLot_6MO, "col_13", v.Global.fLot_13MO)
	f.Data.DataTable.AddRow("displayDT", "type_Col", "$ Var to LOTFACTOR", "col_1", v.Global.fVar_1MO, "col_6", v.Global.fVar_6MO, "col_13", v.Global.fVar_13MO)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.refreshValues.End

Program.Sub.saveToDB.Start
f.Intrinsic.Control.Try
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.SaveToDB("saveDT", "con", "GCG_5542_BOM_CALC", "BomParent", 256)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.saveToDB.End













